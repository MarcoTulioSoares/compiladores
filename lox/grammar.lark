?start     : program

program    : stmt*

?stmt      : expr ";"
           | print_cmd
           | block
           | if_cmd
           // | for_cmd
           // | ...

print_cmd  : "print" expr ";"

block      : "{" stmt* "}"

if_cmd     : "if" "(" expr ")" stmt ["else" stmt]

// EXPRESSÃ•ES
?expr      : test

?test      : test "==" cmp  -> eq
           | test "!=" cmp  -> ne
           | cmp

?cmp       : cmp ">" factor   -> gt
           | cmp "<" factor   -> lt
           | cmp ">=" factor  -> ge
           | cmp "<=" factor  -> le
           | factor

?factor    : factor "+" term  -> add
           | factor "-" term  -> sub
           | term

?term      : term "*" atom   -> mul
           | term "/" atom   -> div 
           | atom

?atom      : NUMBER 
           | BOOL
           | VAR
           | STRING
           | NIL
           | "(" cmp ")"
           | call
           | unary

call       : VAR "(" params ")"
params     : [ expr ("," expr )* ]

unary     : ("!" | "-") atom

BOOL.2     : "true" | "false"
VAR.1      : /[a-z_]\w*/
NUMBER     : /([1-9][0-9]*|0)(\.[0-9]+)?/ 
STRING     : /"[^"]*"/
NIL.2      : "nil"
COMMENT    : "//" /[^\n]*/

%ignore /\s/ | COMMENT